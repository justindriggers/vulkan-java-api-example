import org.gradle.internal.os.OperatingSystem

plugins {
    id 'application'
}

mainClassName = 'com.justindriggers.example.Application'

group = 'com.justindriggers'
version = '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

project.ext.lwjglVersion = "3.2.0"
project.ext.hasVulkanNatives = false

switch ( OperatingSystem.current() ) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        project.ext.hasVulkanNatives = true
        break
}

repositories {
    mavenCentral()
    maven {
        url = 'https://dl.bintray.com/justindriggers/vulkan-java-api'
    }
}

dependencies {
    implementation('com.justindriggers:vulkan-java-api:0.2.0')

    implementation("org.lwjgl:lwjgl:$lwjglVersion:$lwjglNatives")
    implementation("org.lwjgl:lwjgl-glfw:$lwjglVersion:$lwjglNatives")

    if (project.ext.hasVulkanNatives) {
        implementation("org.lwjgl:lwjgl-vulkan:$lwjglVersion:$lwjglNatives")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}
